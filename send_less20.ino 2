#include <ArduinoBLE.h>
#include <Wire.h>
#include "LSM6DS3.h"

// Create a instance of class LSM6DS3
LSM6DS3 myIMU(I2C_MODE, 0x6A);    // I2C device address 0x6A
BLEService imuService("180F"); // Create BLE Service with a custom UUID

BLEStringCharacteristic sensorCharacteristic("2A19", BLERead | BLENotify, 99); // Create BLE Characteristic for Sensor data

void setup() {
  Serial.begin(9600);
  while (!Serial);

  // Call .begin() to configure the IMUs
  if (myIMU.begin() != 0) {
    Serial.println("Device error");
  } else {
    Serial.println("Device OK!");
  }

  // Begin BLE
  if (!BLE.begin()) {
    Serial.println("starting BLE failed!");
    while (1);
  }

  BLE.setLocalName("IMU");
  BLE.setAdvertisedService(imuService);
  imuService.addCharacteristic(sensorCharacteristic);
  BLE.addService(imuService);
  BLE.advertise();
}

void loop() {
  BLEDevice central = BLE.central();
  
  if (central) {
    Serial.print("Connected to central: ");
    Serial.println(central.address());

    while (central.connected()) {
      String sensorData = "";

      sensorData += "Accelerometer: ";
      sensorData += "X=" + String(myIMU.readFloatAccelX(), 4);
      sensorData += " Y=" + String(myIMU.readFloatAccelY(), 4);
      sensorData += " Z=" + String(myIMU.readFloatAccelZ(), 4);

      sensorData += "\nGyroscope: ";
      sensorData += "X=" + String(myIMU.readFloatGyroX(), 4);
      sensorData += " Y=" + String(myIMU.readFloatGyroY(), 4);
      sensorData += " Z=" + String(myIMU.readFloatGyroZ(), 4);

      sensorData += "\nThermometer: ";
      sensorData += "Degrees C=" + String(myIMU.readTempC(), 4);

      Serial.println(sensorData);

      if (sensorData.length() > sensorCharacteristic.valueSize()) {
        sensorData = sensorData.substring(0, sensorCharacteristic.valueSize());
      }

      sensorCharacteristic.setValue(sensorData);
      delay(1000);
    }
  }
}